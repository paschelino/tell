group 'de.paschelino'
version '0.0.1-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.2.10'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.junit.platform:junit-platform-gradle-plugin:1.0.2"
    }
}


apply plugin: "kotlin"
apply plugin: "org.junit.platform.gradle.plugin"
apply plugin: "jacoco"

repositories {
    mavenCentral()
    jcenter()
    maven { url "http://dl.bintray.com/jetbrains/spek" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    testCompile "org.hamcrest:hamcrest-junit:2.0.0.0"

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testCompile ('org.jetbrains.spek:spek-api:1.1.5') {
        exclude group: 'org.jetbrains.kotlin'
    }
    testRuntime ('org.jetbrains.spek:spek-junit-platform-engine:1.1.5') {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

junitPlatform {
    // logManager 'org.apache.logging.log4j.jul.LogManager'
    reportsDir file('build/test-results/junit-platform') // this is the default
    enableStandardTestTask true
    // selectors (optional)

    filters {
        engines {
            include 'spek'
        }
    }
}

project.afterEvaluate {
    def junitPlatformTestTask = project.tasks.getByName('junitPlatformTest')

    jacoco {
        toolVersion = "+"
        reportsDir = file("$buildDir/reports/jacoco")
        applyTo junitPlatformTestTask
    }

    project.task(type: JacocoReport, "junitPlatformJacocoReport", {
        sourceDirectories = files("src/main/kotlin")
        classDirectories = files("$buildDir/classes/kotlin/main")
        reports {
            xml.enabled = false
            csv.enabled = false
            html.enabled = true
            html.destination = new File("${buildDir}/reports/jacoco/test/jacocoTestReport.html")
        }
        executionData junitPlatformTestTask
    })
}
